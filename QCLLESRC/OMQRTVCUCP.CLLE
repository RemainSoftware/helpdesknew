 /* ---------------------------------------------------------------- */
 /*  The MIT License (MIT)                                           */
 /*                                                                  */
 /*  Copyright (c) 2019 Remain (https://remainsoftware.com)          */
 /*                                                                  */
 /*  Permission is hereby granted, free of charge, to any person     */
 /*  obtaining a copy of this software and associated documentation  */
 /*  files (the "Software"), to deal in the Software without         */
 /*  restriction, including without limitation the rights to use,    */
 /*  copy, modify, merge, publish, distribute, sublicense, and/or    */
 /*  sell copies of the Software, and to permit persons to whom      */
 /*  the Software is furnished to do so, subject to the following    */
 /*  conditions:                                                     */
 /*                                                                  */
 /*  The above copyright notice and this permission notice shall     */
 /*  be included in all copies or substantial portions of the        */
 /*  Software.                                                       */
 /*                                                                  */
 /*  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY       */
 /*  KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE      */
 /*  WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR         */
 /*  PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS      */
 /*  OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR        */
 /*  OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR      */
 /*  OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       */
 /*  SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.          */
 /*                                                                  */
 /*  Contributors:                                                   */
 /*      Remain Software - initial API and implementation            */
 /*                                                                  */
 /* ---------------------------------------------------------------- */


 /* ---------------------------------------------------------------- */
 /*                                                                  */
 /*  Program ....: OMQRTVCUCP                                        */
 /*  Description : CPP for Retrieve Customer                         */
 /*  Author .....: Remain Software                                    */
 /*  Module .....: Kernel                                            */
 /*  Date .......: 25 02 1997                                        */
 /*                                                                  */
 /*  Input ......: &CUSC       - Customer                            */
 /*                                                                  */
 /*  Output .....: &NAMD       - Name                                */
 /*                &ADST       - Address                             */
 /*                &ZIPC       - Zipcode                             */
 /*                &ADRE       - Adress                              */
 /*                &COUD       - Country                             */
 /*                &USF1       - User field 1                        */
 /*                &USF2       - User field 2                        */
 /*                &USF3       - User field 3                        */
 /*                                                                  */
 /*  Description : This program is the command processor for         */
 /*  program OMQRTVCU. This command allows the user to retrieve      */
 /*  CUS attributes from a CUS in the CUS file.                      */
 /*                                                                  */
 /* ---------------------------------------------------------------- */
             PGM        PARM(&CUSC &NAMD &ADST &ZIPC &ADRE &COUD +
                          &USF1 &USF2 &USF3)

 /* ---------------------------------------------------------------- */
 /* Declare the varaibles                                            */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&CUSC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NAMD) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ADST) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ZIPC) TYPE(*CHAR) LEN(25)
             DCL        VAR(&ADRE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COUD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USF1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USF2) TYPE(*CHAR) LEN(25)
             DCL        VAR(&USF3) TYPE(*DEC)  LEN(15 0)


 /* ---------------------------------------------------------------- */
 /* Declare the parameters for OMQRTVCC                              */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&C@CUSC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@NAMD) TYPE(*CHAR) LEN(50)
             DCL        VAR(&C@ADST) TYPE(*CHAR) LEN(50)
             DCL        VAR(&C@ZIPC) TYPE(*CHAR) LEN(25)
             DCL        VAR(&C@ADRE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&C@COUD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@USF1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@USF2) TYPE(*CHAR) LEN(25)
             DCL        VAR(&C@USF3) TYPE(*DEC)  LEN(15 0)
             DCL        VAR(&C@ERR)  TYPE(*CHAR) LEN(136)

/* ----------------------------------------------------------------- */
/* Program initialisation                                            */
/* ----------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Install the error monitor                               */
          /* ------------------------------------------------------- */
             MONMSG     MSGID(CPF0000 MCH0000) EXEC(GOTO CMDLBL(ERROR))

 /* ---------------------------------------------------------------- */
 /* Call the retrieve API                                            */
 /* ---------------------------------------------------------------- */
             CALL       PGM(OMQRTVCU) PARM(&CUSC &C@NAMD &C@ADST +
                          &C@ZIPC &C@ADRE &C@COUD &C@USF1 &C@USF2 +
                          &C@USF3 &C@ERR '1000000000000000')

/* ----------------------------------------------------------------- */
/* Fill the output parameters if no error.                           */
/* ----------------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *NE 'E') THEN(DO)

             CHGVAR     VAR(&NAMD) VALUE(&C@NAMD)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&ADST) VALUE(&C@ADST)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&ZIPC) VALUE(&C@ZIPC)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&ADRE) VALUE(&C@ADRE)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&COUD) VALUE(&C@COUD)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF1) VALUE(&C@USF1)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF2) VALUE(&C@USF2)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF3) VALUE(&C@USF3)
             MONMSG     MSGID(MCH3601)

             ENDDO

/* ----------------------------------------------------------------- */
/* Handle return messages                                            */
/* ----------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Clear this programs message queue                       */
          /* ------------------------------------------------------- */
             RMVMSG     CLEAR(*ALL)

          /* ------------------------------------------------------- */
          /* Error                                                   */
          /* ------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *EQ 'E') THEN(DO)
             SNDPGMMSG  MSGID(%SST(&C@ERR 1 7)) MSGF(OMSAPI) +
                          MSGDTA(%SST(&C@ERR 9 128)) MSGTYPE(*ESCAPE)
             ENDDO

          /* ------------------------------------------------------- */
          /* Warning                                                 */
          /* ------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *EQ 'W') THEN(DO)
             SNDPGMMSG  MSGID(%SST(&C@ERR 1 7)) MSGF(OMSAPI) +
                          MSGDTA(%SST(&C@ERR 9 128)) MSGTYPE(*COMP)
             ENDDO

/* ----------------------------------------------------------------- */
/* Exit.                                                             */
/* ----------------------------------------------------------------- */
             GOTO       CMDLBL(ENDPGM)

/* ----------------------------------------------------------------- */
/* An error occured                                                  */
/* ----------------------------------------------------------------- */
 ERROR:
             SNDPGMMSG  MSGID(OMQ5012) MSGF(OMSAPI) +
                          MSGDTA('OMQRTVCU') MSGTYPE(*ESCAPE)

/* ----------------------------------------------------------------- */
/* End of Program                                                    */
/* ----------------------------------------------------------------- */
 ENDPGM:
             ENDPGM
