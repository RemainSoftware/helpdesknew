 /* ---------------------------------------------------------------- */
 /*  The MIT License (MIT)                                           */
 /*                                                                  */
 /*  Copyright (c) 2019 Remain (https://remainsoftware.com)          */
 /*                                                                  */
 /*  Permission is hereby granted, free of charge, to any person     */
 /*  obtaining a copy of this software and associated documentation  */
 /*  files (the "Software"), to deal in the Software without         */
 /*  restriction, including without limitation the rights to use,    */
 /*  copy, modify, merge, publish, distribute, sublicense, and/or    */
 /*  sell copies of the Software, and to permit persons to whom      */
 /*  the Software is furnished to do so, subject to the following    */
 /*  conditions:                                                     */
 /*                                                                  */
 /*  The above copyright notice and this permission notice shall     */
 /*  be included in all copies or substantial portions of the        */
 /*  Software.                                                       */
 /*                                                                  */
 /*  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY       */
 /*  KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE      */
 /*  WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR         */
 /*  PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS      */
 /*  OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR        */
 /*  OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR      */
 /*  OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       */
 /*  SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.          */
 /*                                                                  */
 /*  Contributors:                                                   */
 /*      Remain Software - initial API and implementation            */
 /*                                                                  */
 /* ---------------------------------------------------------------- */


 /* ---------------------------------------------------------------- */
 /* Description                                                      */
 /* ---------------------------------------------------------------- */
 /*                                                                  */
 /* Program ....: OMH039C3                                           */
 /* Function ...: Send definitions                                   */
 /* Author .....: Remain Software                                    */
 /* Parameter ..: &HEDC  - Helpdesk code, used for authorisation     */
 /*               &CUSC  - Customer                                  */
 /*               &EEMC  - Customer email address                    */
 /*               &EXTI  - External interface ('1'=Notes)            */
 /*               &STAT  - Status                                    */
 /*                                                                  */
 /* This program sends the definition data via a mail template to a  */
 /* specific e-mail address. The mail template values are taken from */
 /* the system mail extensions files.                                */
 /*                                                                  */
 /* ---------------------------------------------------------------- */

 /* ---------------------------------------------------------------- */
 /* Program                                                          */
 /* ---------------------------------------------------------------- */

             PGM        PARM(&HEDC &CUSC &EEMC &EXTI &STAT)

          /* ------------------------------------------------------ */
          /* Parameters                                             */
          /* ------------------------------------------------------ */
             DCL        VAR(&HEDC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&CUSC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&EEMC) TYPE(*CHAR) LEN(50)
             DCL        VAR(&EXTI) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(5)

          /* ------------------------------------------------------ */
          /* Variables                                              */
          /* ------------------------------------------------------ */
             DCL        VAR(&SHID) TYPE(*CHAR) LEN(80)
             DCL        VAR(&EMAC) TYPE(*CHAR) LEN(50)
             DCL        VAR(&BCCE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SRCQ) TYPE(*CHAR) LEN(20)

          /* ------------------------------------------------------ */
          /* Install error monitor                                  */
          /* ------------------------------------------------------ */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))

 /* ---------------------------------------------------------------- */
 /* Start of program                                                 */
 /* ---------------------------------------------------------------- */

          /* ------------------------------------------------------ */
          /* Create a sourcefile in qtemp                           */
          /* ------------------------------------------------------ */
             CLRPFM     FILE(QTEMP/OMMTO)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CPYF       FROMFILE(*LIBL/OMMTO) TOFILE(QTEMP/OMMTO) +
                          MBROPT(*NONE) CRTFILE(*YES)
             CLRPFM     FILE(QTEMP/OMMTO)
             ENDDO

          /* ------------------------------------------------------ */
          /* Override to the correct output file                    */
          /* ------------------------------------------------------ */
             OVRDBF     FILE(OMMTO) TOFILE(QTEMP/OMMTO)

 /* ---------------------------------------------------------------- */
 /* Lotus notes                                                      */
 /* ---------------------------------------------------------------- */
             IF         COND(&EXTI *EQ '1') THEN(DO)

 /* ---------------------------------------------------------------- */
 /* Send the customer database.                                      */
 /* ---------------------------------------------------------------- */

             OVRDBF     FILE(OMMTI) TOFILE(*LIBL/OMMTI) MBR(NOTES_CUS1)

          /* ------------------------------------------------------ */
          /* Process the template                                   */
          /* ------------------------------------------------------ */
             CALL       PGM(OMH036) PARM(&HEDC ' ' &CUSC '0' &STAT)
             CALL       PGM(OMH036)

          /* ------------------------------------------------------ */
          /* Send the mail                                          */
          /* ------------------------------------------------------ */
             CHGVAR     VAR(&SRCQ) VALUE('OMMTO     QTEMP     ')
             CHGVAR     VAR(&EMAC) VALUE('*DFT')
             CHGVAR     VAR(&SHID) VALUE('OMH039C3')
             CALL       PGM(OMH032C) PARM('*DFT' '*DFT' &EMAC &EEMC +
                          &BCCE &SHID '*PLAIN' &SRCQ 'OMMTO     ' +
                          &HEDC ' ')
             ENDDO

 /* ---------------------------------------------------------------- */
 /* End processing                                                   */
 /* ---------------------------------------------------------------- */

          /* ------------------------------------------------------ */
          /* Remove the overrides                                   */
          /* ------------------------------------------------------ */
             DLTOVR     FILE(*ALL)

          /* ------------------------------------------------------ */
          /* Exit                                                   */
          /* ------------------------------------------------------ */
             GOTO       CMDLBL(ENDPGM)

 /* ---------------------------------------------------------------- */
 /* Handle unexpected errors                                         */
 /* ---------------------------------------------------------------- */
 ERROR:
             CHGVAR     VAR(&STAT) VALUE('*TERM')
             MONMSG     MSGID(CPF0000)
             RETURN

 /* ---------------------------------------------------------------- */
 /* Exit program processing                                          */
 /* ---------------------------------------------------------------- */
 ENDPGM:     RMVMSG     CLEAR(*ALL)
             MONMSG     MSGID(CPF0000)
             ENDPGM

