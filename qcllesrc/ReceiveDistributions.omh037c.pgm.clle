 /* ---------------------------------------------------------------- */
 /*  The MIT License (MIT)                                           */
 /*                                                                  */
 /*  Copyright (c) 2019 Remain (https://remainsoftware.com)          */
 /*                                                                  */
 /*  Permission is hereby granted, free of charge, to any person     */
 /*  obtaining a copy of this software and associated documentation  */
 /*  files (the "Software"), to deal in the Software without         */
 /*  restriction, including without limitation the rights to use,    */
 /*  copy, modify, merge, publish, distribute, sublicense, and/or    */
 /*  sell copies of the Software, and to permit persons to whom      */
 /*  the Software is furnished to do so, subject to the following    */
 /*  conditions:                                                     */
 /*                                                                  */
 /*  The above copyright notice and this permission notice shall     */
 /*  be included in all copies or substantial portions of the        */
 /*  Software.                                                       */
 /*                                                                  */
 /*  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY       */
 /*  KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE      */
 /*  WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR         */
 /*  PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS      */
 /*  OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR        */
 /*  OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR      */
 /*  OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       */
 /*  SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.          */
 /*                                                                  */
 /*  Contributors:                                                   */
 /*      Remain Software - initial API and implementation            */
 /*                                                                  */
 /* ---------------------------------------------------------------- */


 /* ---------------------------------------------------------------- */
 /* Description                                                      */
 /* ---------------------------------------------------------------- */
 /*                                                                  */
 /* Program ....: OMH037C                                            */
 /* Function ...: Receive distributions                              */
 /* Author .....: Remain Software                                    */
 /* Parameter ..: &USRI  - SNADS user id                             */
 /*               &ADDR  - SNADS address                             */
 /*               &STAT  - Status                                    */
 /*                                                                  */
 /* This program receives the distributions for a given user and     */
 /* processes them.                                                  */
 /*                                                                  */
 /* ---------------------------------------------------------------- */

             PGM        PARM(&USRI &ADDR &STAT)

 /* ---------------------------------------------------------------- */
 /* Parameters                                                       */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&USRI) TYPE(*CHAR) LEN(8)
             DCL        VAR(&ADDR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(5)

 /* ---------------------------------------------------------------- */
 /* Variables                                                        */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&MSGD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&NULL) TYPE(*CHAR) LEN(4) +
                          VALUE(X'00000000')

 /* ---------------------------------------------------------------- */
 /* Files                                                            */
 /* ---------------------------------------------------------------- */
             DCLF       FILE(OMDIQ)

 /* ---------------------------------------------------------------- */
 /* Install global error monitor                                     */
 /* ---------------------------------------------------------------- */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))

 /* ---------------------------------------------------------------- */
 /* Check if there are distributions for this user.                  */
 /* ---------------------------------------------------------------- */
             QRYDST     USRID(&USRI &ADDR)
             RCVMSG     MSGDTA(&MSGD)
             IF         COND(&MSGD *NE &NULL) THEN(DO)

 /* ---------------------------------------------------------------- */
 /* Receive the distributions in a file                              */
 /* ---------------------------------------------------------------- */
             DLTF       FILE(QTEMP/OMDIQ)
             MONMSG     MSGID(CPF0000)

             QRYDST     USRID(&USRI &ADDR) OUTFILE(QTEMP/OMDIQ)

          /* ------------------------------------------------------ */
          /* Read that file and get the data in OMERF               */
          /* ------------------------------------------------------ */
             DLTF       FILE(QTEMP/OMERF)
             MONMSG     MSGID(CPF0000)

             OVRDBF     FILE(OMDIQ) TOFILE(QTEMP/OMDIQ)

             RCVF
             RCVDST     DSTID(&LINDID) USRID(&USRI &ADDR) +
                          OUTFILE(QTEMP/OMERF) OUTDTATYP(*DOC)

 MH03710:    RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(LEAVE10))
             RCVDST     DSTID(&LINDID) USRID(&USRI &ADDR) +
                          OUTFILE(QTEMP/OMERF) OUTMBR(*FIRST *ADD) +
                          OUTDTATYP(*DOC)
             GOTO       CMDLBL(MH03710)


 LEAVE10:
          /* ------------------------------------------------------ */
          /* Remove the userspace                                   */
          /* ------------------------------------------------------ */
             DLTUSRSPC  USRSPC(QTEMP/OMH037)
             MONMSG     MSGID(CPF0000)

          /* ------------------------------------------------------ */
          /* OMERF read, fill the HelpDesk files                    */
          /* ------------------------------------------------------ */
             OVRDBF     FILE(OMERF) TOFILE(QTEMP/OMERF)
             CALL       PGM(OMH037) PARM('1' &STAT)

             ENDDO


 /* ---------------------------------------------------------------- */
 /* Goto end of program                                              */
 /* ---------------------------------------------------------------- */
             GOTO       CMDLBL(ENDPGM)

 /* ---------------------------------------------------------------- */
 /* Handle unexpected errors                                         */
 /* ---------------------------------------------------------------- */
 ERROR:
             CHGVAR     VAR(&STAT) VALUE('*TERM')

 /* ---------------------------------------------------------------- */
 /* Exit program processing                                          */
 /* ---------------------------------------------------------------- */
 ENDPGM:     RMVMSG     CLEAR(*ALL)

             ENDPGM
